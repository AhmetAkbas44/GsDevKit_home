TDScriptLeafNode{#name:'postUpgradeSortedCollections',#contents:'[ :topez :objIn :tokens :command :commandNode | 
  | opts args |
  \"for help: ./postUpgradeSortedCollections -h\"
  command
    getOptsMixedLongShort:
      {#(\'help\' $h #\'none\').
      #(\'sourceVersion\' nil #\'required\')}
    optionsAndArguments: [ :options :operands | 
      opts := options.
      args := operands ].
  opts
    at: \'help\'
    ifAbsent: [ 
      | gsTool sourceStoneVersion currentStoneVersion report |
      report := \'No SortedCollection instances found\'.
      gsTool := topez toolInstanceFor: \'gs\'.
      currentStoneVersion := ((gsTool gsversion: #\'stone\') at: \'gsVersion\')
        asMetacelloVersionNumber.
      opts
        at: \'sourceVersion\'
        ifPresent: [ :sourceVersionString | sourceStoneVersion := sourceVersionString asMetacelloVersionNumber ]
        ifAbsent: [ self error: \'Required option --sourceVersion not present\' ].
      (currentStoneVersion > \'3.3.0\' asMetacelloVersionNumber
        and: [ sourceStoneVersion < \'3.3.0\' asMetacelloVersionNumber ])
        ifTrue: [ 
          | userId stream upgradeLogDir |
          \"Find all instances of SortedCollection, recompile sortBlocks\"
          userId := GsCurrentSession currentSession userProfile userId.
          System commit.
          SortedCollection findAllSortedCollectionsForUser: userId.
          SortedCollection initializeForConversion.
          SortedCollection convertInstancesFromFilesForGem: 1 of: 1.
          SortedCollection writeTotalsFiles.
          upgradeLogDir := ServerFileDirectory
            on: (System gemEnvironmentVariable: \'upgradeLogDir\').
          stream := WriteStream on: String new.
          stream nextPutAll: \'SortedCollection conversion results: \'.
          upgradeLogDir
            readOnlyFileNamed: \'sc_ok.txt\'
            do: [ :fStream | stream nextPutAll: fStream contents ].
          stream nextPutAll: \' sortBlocks recompiled successfully, \'.
          upgradeLogDir
            readOnlyFileNamed: \'sc_failed.txt\'
            do: [ :fStream | stream nextPutAll: fStream contents ].
          stream nextPutAll: \' sortBlocks had errors, \'.
          SortedCollection createConversionResultFileForTotalGems: 1.
          report := stream contents ].
      report ]
    ifPresent: [ :ignored | 
      TDManPage
        viewManPage:
          \'NAME
  postUpgradeSortedCollections - Post-upgrade recompile sortBlocks for SortedCollections
SYNOPSIS
  postUpgradeSortedCollections [-h|--help] --sourceVersion=<source-gemstone-version>

DESCRIPTION
  Recompile simple sortBlocks for SortedCollections as described in BugNote 46055 (https://gemtalksystems.com/data/bugnotes/46055.html).

  The environment variable $upgradeLogDir is expected to be set -- normally set by 
  $GS_HOME/bin/upgradeStone.
EXAMPLES
  ./postUpgradeSortedCollections -h

  ./postUpgradeSortedCollections --sourceVersion=3.2.9 
\'
        topez: topez ] ]',#creationTime:DateAndTime['2016-05-04T19:38:57.28930902481079-07:00'],#modificationTime:DateAndTime['2016-05-10T12:14:42.18962597846985-07:00']}