pre-upgrade tasks
preUpgradeUnicodeLibrariesEnvironmentVariable
	"Copy the source icu libraries to the target stone's lib directory, using the target stone's libary naming convention.
	 Use env var to install the new icu libraries."

	| targetLibDir sourceLibDir envFile contents |
	sourceLibDir := self gs_stonesHome / self sourceStoneName / 'product' / 'lib'.
	targetLibDir := self gs_stonesHome / self targetStoneName / 'product' / 'lib'.
	OSProcess command: 'chmod +w ' , targetLibDir pathString.
	self libicuBasenames
		do: [ :base | 
			| targetSourceLibFile sourceLibFile targetSource |
			sourceLibFile := self libicuFileNameFor: base version: self sourceGsVers for: self sourceGsVers.
			targetSourceLibFile := self libicuFileNameFor: base version: self sourceGsVers for: self targetGsVers.
			targetSource := targetLibDir / targetSourceLibFile.
			targetSource exists
				ifFalse: [ 
					self
						runShellCommand: '/bin/cp'
						args:
							{((sourceLibDir / sourceLibFile) pathString).
							(targetSource pathString)} ] ].
	envFile := self gs_stonesHome / self targetStoneName / 'custom_stone.env'.
	envFile readStreamDo: [ :stream | contents := stream contents ].
	envFile
		writeStreamDo: [ :stream | 
			| icuPrefix |
			"if the source stone is using GS_LIB_ICU_PREFIX, then we need to carry forward the prefixed libraries"
			icuPrefix := Smalltalk os environment at: 'GS_LIB_ICU_PREFIX' ifAbsent: [ 'libicu-' , self sourceGsVers printString ].
			stream
				nextPutAll: contents;
				lf;
				nextPutAll: '# Generated by devKitCommandLine preUpgradeStone command';
				lf;
				nextPutAll: 'export GS_LIB_ICU_PREFIX="' , icuPrefix , '"';
				lf ]